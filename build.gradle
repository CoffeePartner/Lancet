// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'base'
buildscript {
    ext {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())

        if (!ele) {
            bintrayUser = properties.getProperty('bintrayUser')
            bintrayKey = properties.getProperty('bintrayKey')
        }
        lancet_group = 'com.dieyidezui'
        lancet_version = '2.0.0-SNAPSHOT'
        //lancet_version = '0.0.1.8-SNAPSHOT'

        asm_version = '7.0'
        android_tools_version = '3.2.1'
        guava_version = '23.0'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://dl.bintray.com/dieyidezui/maven"
        }
    }
    dependencies {

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects { pro ->

    repositories {
        jcenter()
        mavenCentral()
        google()
    }

    version = lancet_version
    group = lancet_group


    apply plugin: 'maven'
    apply plugin: 'com.jfrog.bintray'
    afterEvaluate {
        task sourcesJar(type: Jar) {
            from sourceSets.main.java.srcDirs
            classifier = 'sources'
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives sourcesJar
            archives javadocJar
        }
        tasks.install {
            repositories.mavenInstaller {
                pom {
                    project {
                        packaging 'jar'
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id 'dieyidezui'
                                name '耿万鹏'
                                email 'dieyidezui@gmail.com'
                            }
                            developer {
                                id 'jude95'
                                name '朱晨曦'
                                email '973829691@qq.com'
                            }
                        }
                    }
                }
            }
        }
    }
    bintray {
        user = bintrayUser
        key = bintrayKey
        configurations = ['archives']
        pkg {
            repo = "maven"
            name = pro.name    //发布到JCenter上的项目名字
            websiteUrl = "https://github.com/CoffeePartner/Lancet"
            vcsUrl = "git@github.com:CoffeePartner/Lancet.git"
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}

}

task uploadAll { task ->

    task.group = 'upload'
    gradle.projectsEvaluated {
        task.dependsOn project.tasks.clean
        task.dependsOn project.tasks.build

        project.tasks.build.mustRunAfter project.tasks.clean

        project.subprojects {
            Task upload = it.tasks.bintrayUpload
            task.dependsOn upload
            upload.mustRunAfter project.tasks.build
        }
    }
}
